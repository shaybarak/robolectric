apply plugin: ShadowsPlugin

shadows {
    packageName "org.robolectric.shadows.httpclient"
}

configurations {
    earlyRuntime
}

dependencies {
    // Project dependencies
    compile project(":robolectric")
    compile project(":robolectric-shadows/shadows-core")

    // Compile dependencies
    earlyRuntime "org.apache.httpcomponents:httpcore:4.0.1"
    compile "org.apache.httpcomponents:httpclient:4.0.3"
    compileOnly(AndroidSdk.LOLLIPOP_MR1.coordinates) { force = true }

    // Testing dependencies
    testCompile "junit:junit:4.8.2"
    testCompile "org.hamcrest:hamcrest-core:1.3"
    testCompile "org.assertj:assertj-core:2.0.0"
    testCompile "org.mockito:mockito-core:1.8.0"
    testRuntime AndroidSdk.LOLLIPOP_MR1.coordinates
}

// change local artifact name to match dependencies
install {
    repositories.mavenInstaller {
        pom.artifactId = 'shadows-httpclient'
    }
}

// httpcore needs to come before android-all on runtime classpath; the gradle IntelliJ plugin
//   needs the compileClasspath order patched too (bug?)
sourceSets.main.compileClasspath = configurations.earlyRuntime + sourceSets.main.compileClasspath
sourceSets.main.runtimeClasspath = configurations.earlyRuntime + sourceSets.main.runtimeClasspath

sourceSets.test.compileClasspath = configurations.earlyRuntime + sourceSets.test.compileClasspath
sourceSets.test.runtimeClasspath = configurations.earlyRuntime + sourceSets.test.runtimeClasspath